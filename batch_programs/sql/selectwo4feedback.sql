/*********************************************************************
*
* PROGRAM NAME          : SELECTWO4FEEDBAC.SQL
*
*
* DESCRIPTION           : THIS PL/SQL SCRIPT SELECTS COMPLATED WORK ORDERS
*                         FOR FEEDBACK. THE SELECTION IS RANDOM WITH 15% SAMPLE
*
* AUTHOR                : PANKAJ BHIDE
*
* DATE WRITTEN          : 25-JUL-17
*
* DATE MODIFIED         :
*
* MODIFICATION HISTORTY : 
*********************************************************************/
WHENEVER SQLERROR EXIT 1 ROLLBACK;

DECLARE 

    TYPE WOCUR_TYPE IS REF CURSOR;
    WOCUR WOCUR_TYPE;
    WORKORDERS_REC WORKORDER%ROWTYPE;    
    T_ROW_COUNT NUMBER(5) :=0;
    T_PRV_WO1   WORKORDER.WO1%TYPE;
    T_PRV_WONUM WORKORDER.WONUM%TYPE;
    T_SAMPLE    NUMBER(5) :=0;
    T_SAMPLE_PCT    NUMBER(5) :=0;
    T_SAMPLE_S  VARCHAR2(2000);
    T_SELECT    VARCHAR2(20000);
    T_WO_FEEDBACK_WHERE    VARCHAR2(20000);   
    TYPE RANDOM_WOCUR_TYPE IS REF CURSOR;
    RANDOM_WOCUR RANDOM_WOCUR_TYPE;
    RANDOM_WOREC BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST%ROWTYPE;
    
BEGIN 

-- DELETE ALL PRIOR UNPROCESSED RECORDS 
DELETE FROM BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST 
WHERE INSERTDATE IS NULL;

-- GET SAMPLE PERCENTAGE
SELECT TO_NUMBER(VARVALUE) 
INTO  T_SAMPLE_PCT
FROM  LBL_MAXVARS 
WHERE VARNAME='WOFEEDBACK_SAMPLE_PCT';

-- GET WHERE CONDITION FOR SELECTING WORK ORDERS
-- FOR FEEDBACK
SELECT VARVALUE 
INTO T_WO_FEEDBACK_WHERE
FROM LBL_MAXVARS
WHERE VARNAME='WO_FEEDBACK_WHERE';

T_SELECT :='SELECT * FROM WORKORDER A WHERE ' || T_WO_FEEDBACK_WHERE || ' ORDER BY A.WO1, A.WONUM ';

 -- FIRST PASS 
 -- GET 1 WORK ORDER FOR UNIQUE WO1 INSERT ITS NUMBER INTO THE TABLE.
 -- (LIKELY CANDIDATE ) 

OPEN WOCUR FOR T_SELECT;
   
LOOP
      
    FETCH WOCUR INTO WORKORDERS_REC  ;
    
    T_ROW_COUNT := T_ROW_COUNT + 1;
    
    IF (T_ROW_COUNT =1) THEN
      T_PRV_WO1   := WORKORDERS_REC.WO1;
      T_PRV_WONUM := WORKORDERS_REC.WONUM;
    END IF;
    
    IF (T_PRV_WO1 !=WORKORDERS_REC.WO1) THEN
    
       INSERT INTO BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST
       (ORGID, SITEID,WONUM) VALUES
       ('LBNL','FAC',T_PRV_WONUM);
        T_PRV_WO1   := WORKORDERS_REC.WO1;
        T_PRV_WONUM := WORKORDERS_REC.WONUM;
    END IF;
    
     EXIT WHEN WOCUR%NOTFOUND;
    
 END LOOP;
      -- LAST RECORD 
      IF (T_PRV_WONUM IS NOT NULL) THEN 
        INSERT INTO BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST
         (ORGID, SITEID,WONUM) VALUES
         ('LBNL','FAC',T_PRV_WONUM);
      END IF;
       
  -- SECOND PASS
  -- NOW GET 15% OF THE SAMPLE DATA FROM THE ABOVE ROWS
    
  -- SET THE VALUE OF INSERTDATE TO SYSDATE SO THAT THEY CAN BE USED FOR 
  -- FEEDBACK REQUEST.
  -- WINNING CANDIDATE 
 
   IF (T_ROW_COUNT > 0) THEN
   
     T_SAMPLE := T_ROW_COUNT*(T_SAMPLE_PCT/100); -- SAMPLE SIZE 
     
     IF (T_SAMPLE < 1) THEN
      T_SAMPLE :=1;
     END IF;
     
     T_SAMPLE_S := TRIM(TO_CHAR(T_SAMPLE,'999999'));
      
     T_SELECT := ' SELECT * FROM (SELECT * FROM BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST WHERE INSERTDATE IS NULL ';
     T_SELECT := T_SELECT || '  ORDER BY DBMS_RANDOM.VALUE) WHERE  ROWNUM <= ';
     T_SELECT := T_SELECT || T_SAMPLE_S ;
    
    
     OPEN RANDOM_WOCUR FOR T_SELECT;
   
     LOOP
      
        FETCH RANDOM_WOCUR INTO RANDOM_WOREC;
 
        UPDATE BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST 
        SET INSERTDATE=SYSDATE   
        WHERE WONUM=RANDOM_WOREC.WONUM; -- WINNDER WORK ORDER 
         
        EXIT WHEN RANDOM_WOCUR%NOTFOUND;
      
     END LOOP; 

  CLOSE RANDOM_WOCUR;
  
  -- UNFORUNATE, NOT SELECTED IN THE DRAW, THEREFORE DELETE THEM 
  DELETE BATCH_MAXIMO.LBL_WOFEEDBACK_REQUEST WHERE INSERTDATE IS NULL;
    
 END IF;  --  IF (T_ROW_COUNT > 0)
 
  UPDATE LBL_MAXVARS SET VARVALUE=TO_CHAR(SYSDATE,'YYYY-MM-DD HH24:MI:SS')
  WHERE VARNAME='LAST_WOFEEDBACK_GENERATE';
 

COMMIT;

END;

/

  
     
    
    
